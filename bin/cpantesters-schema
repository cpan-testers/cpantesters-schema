#!/usr/bin/env perl
# ABSTRACT: Install, upgrade and examine the CPAN Testers database schema
# PODNAME: cpantesters-schema
our $VERSION = '0.029';

=head1 SYNOPSIS

    # prepare a new upgrade script
    cpantesters-schema prepare --preversion <version>

    # install a new database
    cpantesters-schema install

    # install a database to the given DSN
    cpantesters-schema install dbi:SQLite:local.db

    # upgrade an existing database
    cpantesters-schema upgrade

    # check what version our database is running
    cpantesters-schema check

    # fetch data from the CPAN Testers API to populate our database
    cpantesters-schema fetch report --dist Yancy@1.023

=head1 DESCRIPTION

This script works with L<DBIx::Class::Schema::Versioned> to prepare a new
database upgrade script, install the database, upgrade the database, or
check the database version and available upgrades.

=head1 ARGUMENTS

=head2 <command>

The command to run. One of C<check>, C<install>, C<upgrade>, C<prepare>.

C<check> will show the current database version and the list of
potential versions. C<install> will install a new database from scratch
or prepare an existing database to be upgraded. C<upgrade> will upgrade
the current database. C<prepare> is run during development to prepare
a new upgrade script.

=head1 OPTIONS

=head2 --preversion

The previous version. Used by the C<prepare> command to determine which
upgrade script to make.

=head1 SEE ALSO

L<DBIx::Class::Schema::Versioned>

=cut

use v5.24;
use warnings;
use Pod::Usage;
use Getopt::Long qw( GetOptionsFromArray :config pass_through );
use CPAN::Testers::Schema;
use File::Share qw( dist_dir );
use Path::Tiny qw( path );

my ( $preversion, $help, $dsn, $username, $password, $password_file );
GetOptions(
    'dsn=s' => \$dsn,
    'username=s' => \$username,
    'password=s' => \$password,
    'password-file=s' => \$password_file,
    'p|preversion:s'  => \$preversion,
) or pod2usage(1);

if ($password_file) {
  $password = path($password_file)->slurp;
  chomp $password;
}

my $schema;
if ( $dsn ) {
    my @connect_args = (
      $dsn,
      ($username || $password ? ($username) : ()),
      ($password ? ($password) : ()),
    );
    $schema = CPAN::Testers::Schema->connect( @connect_args );
}
else {
    $schema = CPAN::Testers::Schema->connect_from_config;
}

my %tasks = (
    prepare => \&prepare,
    install => \&install,
    upgrade => \&upgrade,
    check => \&check,
    fetch => \&fetch,
);

my $task = shift @ARGV;
$tasks{ $task }->( @ARGV );

sub prepare {
    my @args = @_;
    my $sql_dir = dist_dir( 'CPAN-Testers-Schema' );
    my $version = $schema->schema_version();
    $schema->create_ddl_dir( 'MySQL', $version, $sql_dir, $preversion );

    $schema = CPAN::Testers::Schema->connect( 'dbi:SQLite::memory:' );
    $schema->create_ddl_dir( 'SQLite', $version, $sql_dir, $preversion );
}

sub check {
    my @args = @_;
    say "  Current: " . $schema->get_db_version;
    say "Available: " . join ", ", $schema->ordered_schema_versions;
}

sub install {
    my @args = @_;
    $schema->install( '0.000' );
    $schema->upgrade;
}

sub upgrade {
    my @args = @_;
    if ( !$schema->get_db_version() ) {
        $schema->deploy;
    }
    else {
        $schema->upgrade;
    }
}

sub fetch {
    my ( @args ) = @_;
    GetOptionsFromArray( \@args, \my %opt,
        'dist|d=s@',
    );
    my @tables = @args;
    if ( !$opt{dist}->@* ) {
        die "Must specify one or more --dist options";
    }
    for my $dist_spec ( $opt{dist}->@* ) {
        my ( $dist, $version ) = split /\@/, $dist_spec;
        $schema->populate_from_api(
            {
                dist => $dist,
                version => $version,
            },
            @tables,
        );
    }
}

